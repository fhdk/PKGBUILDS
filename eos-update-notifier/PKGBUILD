# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-update-notifier
pkgdesc="Software update notifier and 'news for you' for EndeavourOS users."
pkgver=1.14
pkgrel=2
arch=('any')
license=('GPL')
depends=(eos-bash-shared pacman-contrib xdg-utils)
optdepends=(
  "yay:   alternative AUR helper supported by $pkgname"
  "paru:  alternative AUR helper supported by $pkgname"
  "nano:  alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
  "emacs: alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
  "vim:   alternative editor (see EOS_SUDO_EDITORS in /etc/eos-script-lib-yad.conf for more)"
)
backup=(etc/$pkgname.conf)

url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

source=(
  $_url/$pkgname
  $_url/$pkgname.timer
  $_url/$pkgname.service
  $_url/${pkgname}-configure
  $_url/$pkgname.completion
  $_url/$pkgname.conf
#  $_url/arch-news-for-you
  $_url/eos-arch-news
  $_url/$pkgname.desktop
  $_url/${pkgname}-configure.desktop
  $_url/$pkgname.timer
  $_url/$pkgname.service
  $_url/checkupdatesext
)
sha512sums=('c10a7df197239902c8f5120052694174e5b6a783ad2b027420a95be3f0baa0d9711f7e558581c2ff15381a1a47add5ed39785364d992029d1679d59237d8f957'
            '36be7ef9b26b5d0282ee714843cb955db20487face5c1b7885d8fa0f98fc433288a84b357c65a245385d966b37610e394d6a88dd498c27417bf1bf6dab1a1751'
            'd046e675f98a52b9c310e970c5a0b0bc92b08b60d55b9d39140516977e8a6ceb4b3f9d9b05d12fce377b5744f4563e5e66a86ceecafb57f5bd486f968dc57234'
            'd517421590089837dc1c1b3008385f3745e9cd40f06104c4c87caef5ec06e972894a74ff1c6554cff5ec8bb66299d85cc1ed2889f3b7564ceaffe296b904b63b'
            'd5d4d92a9fd0d7a4425bb7000d208d717846ff30c2c31abac94c88840d3e8edc4862a6aacbb576330ff9d7644e63cf90e509caad629d9ed516f378e62b26e168'
            '18e58925ad3f7e5d51b4038d59255d1f2124e76b93878f7d4ddf721ae410a58a9ab7fc707f51074e6eaf298e96f6e18ae81bbfaaaae668187487ba90a9ac444d'
            '174122633d00841322f8b243ebc41f6b2942edb790bb7eefa9469c9bd45ffd51c875373f6c5a71f3f017020f539413e60cd6b8538dba402cfbd831bd5aa24470'
            'a1e17d8f2f1579b000dacdc7a37be698c29528365d9e94d636ceffb3eb8346e16b4ff8ca04ce8ea35d9fff346fe548449d3609f3d448219eb5d89b2c3a5ac571'
            'daa4868b0e2a554990b4091cade1e99f92cf1b450afa5bbe49183e622d53a3994f2227d5d8cf852e22a8c5781c7780433fbf529556131a8b48effe51b9e7e7e1'
            '36be7ef9b26b5d0282ee714843cb955db20487face5c1b7885d8fa0f98fc433288a84b357c65a245385d966b37610e394d6a88dd498c27417bf1bf6dab1a1751'
            'd046e675f98a52b9c310e970c5a0b0bc92b08b60d55b9d39140516977e8a6ceb4b3f9d9b05d12fce377b5744f4563e5e66a86ceecafb57f5bd486f968dc57234'
            '74903e0d815f888c480e515fffd8966b8641f47b28a76c4f4ff58067ec39310278bb8b6a193f82ca1a09271c65c216623d1421da86495111710ff813adedd27b')

package() {
  install -Dm755 $pkgname                      $pkgdir/usr/bin/$pkgname
  install -Dm755 ${pkgname}-configure          $pkgdir/usr/bin/${pkgname}-configure
  install -Dm644 $pkgname.conf                 $pkgdir/etc/$pkgname.conf
#  install -Dm755 arch-news-for-you             $pkgdir/usr/bin/arch-news-for-you
  install -Dm755 eos-arch-news                 $pkgdir/usr/bin/eos-arch-news
  install -Dm755 checkupdatesext               $pkgdir/usr/bin/checkupdatesext
  install -Dm644 $pkgname.desktop              $pkgdir/etc/xdg/autostart/$pkgname.desktop

  install -Dm644 $pkgname.timer                $pkgdir/etc/skel/.config/systemd/user/$pkgname.timer
  install -Dm644 $pkgname.service              $pkgdir/etc/skel/.config/systemd/user/$pkgname.service

  install -Dm644 ${pkgname}-configure.desktop  $pkgdir/usr/share/applications/${pkgname}-configure.desktop

  install -d $pkgdir/usr/share/bash-completion/completions
  install -Dm644 $pkgname.completion $pkgdir/usr/share/bash-completion/completions/$pkgname
}
