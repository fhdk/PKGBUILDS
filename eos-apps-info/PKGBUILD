# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.2.3
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('b94e4993ddc3a02880f2a223f6444318ef6924b4c6a8e01306cd16bd7ee6dd92aa04e0028e87961f5b0be0b27d846ef31af2e4a67bdfae995f7862d515c11df4'
            '34d527b87538cf591bfaa26da735559447deb23e4a17dafe17ded33bebefd0f80b4b9adce88f5b9a5fadb11541f461175d179f385b3a5cc47093d3e4d0215c60'
            '941b642c6003bee67482b22e02751a725add35fe2150ed6ae87488bcf2b4f6657f58a735aeee196ff0f6d160daeb0fa286b2fbd641e40ed3341b2ea35ee9ec2b'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            '4b5eef3ce297cfa66d38b6ad92739eee905050228f72bd2c58d7f05700269b0ef434b0aa5c6ff8423a98af33918089f1255858bd2385ef3db2967d71e0d1d350'
            '7950ea261899b6d11ad9d3ab165f4beac816d543eedb563ee3f93a545a16985adb88f2287feea786c6d3d20e30fb7318422c4e644728d410543e1b975e0b34e8')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
